#!/usr/bin/env perl
use warnings;
use strict;
use v5.10.0;

use open IO => ":locale";

use JSON qw();
use Storable qw(dclone);
use URI::Escape qw(uri_escape_utf8);
use Data::Dumper qw(Dumper);

my $json = JSON->new();
$json->pretty(1);

my $unsafe = '^' . join('', map { quotemeta($_) }
                            grep { index('/\\"*:<>?|', $_) == -1 }
                            map { chr($_) }
                            (32 .. 126));

listBookmarks();
listStations();

sub listBookmarks {
    my $data = readFile("$ENV{HOME}/.pandora/data/bookmarks.json");
    my $artists = delete $data->{artists};
    if (scalar @$artists) {
        say "# Artist Bookmarks";
        say '';
        foreach my $artist (@$artists) {
            delete $artist->{bookmarkToken};
            delete $artist->{dateCreated};
            delete $artist->{artUrl};
            delete $artist->{musicToken};
            my $artistName = delete $artist->{artistName};
            say '-   ' . $artistName;
            if (scalar keys %$artist) {
                say '    -   unprocessed data: ', join(', ', sort keys %$artist);
            }
        }
        say '';
    }
    my $songs = delete $data->{songs};
    if (scalar @$songs) {
        say "# Song Bookmarks";
        say '';
        foreach my $song (@$songs) {
            delete $song->{musicToken};
            delete $song->{sampleUrl};
            delete $song->{dateCreated};
            delete $song->{bookmarkToken};
            delete $song->{sampleGain};
            delete $song->{artUrl};
            my $songName = delete $song->{songName};
            my $artistName = delete $song->{artistName};
            my $albumName = delete $song->{albumName};
            my $line = $artistName . ' -- ' . $songName;
            $line .= ' -- from: ' . $albumName if defined $albumName;
            say '-   ' . $line;
            if (scalar keys %$song) {
                say '    -   unprocessed data: ', join(', ', sort keys %$song);
            }
        }
        say '';
    }
    if (scalar keys %$data) {
        say 'unprocessed bookmark data: ', join(', ', sort keys %$data);
        say '';
    }
}

sub listStations {
    my $data = readFile("$ENV{HOME}/.pandora/data/stations.json");
    delete $data->{checksum};
    my $stations = delete $data->{stations};

    say "# Stations" if scalar @$stations;
    say '';

    foreach my $station (@$stations) {
        my $station = dclone($station);
        delete $station->{allowRename};
        delete $station->{dateCreated};
        delete $station->{stationSharingUrl};
        delete $station->{stationId};
        delete $station->{allowDelete};
        delete $station->{allowEditDescription};
        delete $station->{stationDetailUrl};
        delete $station->{allowAddMusic};
        delete $station->{isShared};
        delete $station->{processSkips};
        my $isThumbprint       = delete $station->{isThumbprint};
        my $thumbCount         = delete $station->{thumbCount};
        my $isQuickMix         = delete $station->{isQuickMix};
        my $token              = delete $station->{stationToken};
        my $isGenreStation     = delete $station->{isGenreStation};
        my $stationName        = delete $station->{stationName};
        my $genre              = delete $station->{genre};
        my $quickMixStationIds = delete $station->{quickMixStationIds};
        my $line = $stationName;
        $line .= ' [Genre Station]'    if $isGenreStation;
        $line .= ' [QuickMix]'         if $isQuickMix && $stationName ne 'QuickMix';
        $line .= ' [Thumbprint Radio]' if $isThumbprint && $stationName ne 'Thumbprint Radio';
        $line .= sprintf(' [%s thumbs]', $thumbCount) if defined $thumbCount;
        say '-   ' . $line;
        say '    station: ', join(', ', sort keys %$station) if scalar keys %$station;
    }
    say '';
    if (scalar keys %$data) {
        say 'unprocessed station list data: ', join(', ', sort keys %$data);
        say '';
    }
    foreach my $station (@$stations) {
        listStationData($station);
    }
}

sub listStationData {
    my ($station) = @_;
    my $token = $station->{stationToken};
    my $name  = $station->{stationName};
    my $nameEscaped = uri_escape_utf8($name, $unsafe);
    my $data = readFile("$ENV{HOME}/.pandora/data/stations/$nameEscaped.json");

    say '## ' . $name;
    say '';

    delete $data->{isShared};
    delete $data->{stationId};
    delete $data->{isQuickMix};
    delete $data->{stationToken};
    delete $data->{isGenreStation};
    delete $data->{stationSharingUrl};
    delete $data->{allowEditDescription};
    delete $data->{allowAddMusic};
    delete $data->{dateCreated};
    delete $data->{allowDelete};
    delete $data->{allowRename};
    delete $data->{artUrl};
    delete $data->{stationDetailUrl};
    delete $data->{stationName};
    delete $data->{processSkips};
    delete $data->{isThumbprint};
    delete $data->{thumbCount};
    delete $data->{quickMixStationIds};

    my $genre    = delete $data->{genre};
    my $feedback = delete $data->{feedback};
    my $music    = delete $data->{music};

    if ($feedback) {
        my $totalThumbsUp = delete $feedback->{totalThumbsUp};
        my $totalThumbsDown = delete $feedback->{totalThumbsDown};
        my $thumbsUp = delete $feedback->{thumbsUp};
        my $thumbsDown = delete $feedback->{thumbsDown};
        if (scalar @$thumbsUp || scalar @$thumbsDown) {
            say '### Feedback';
            say '';
            foreach my $thumb (@$thumbsUp, @$thumbsDown) {
                delete $thumb->{dateCreated};
                delete $thumb->{feedbackId};
                delete $thumb->{songIdentity};
                delete $thumb->{musicToken};
                delete $thumb->{pandoraId};
                delete $thumb->{albumArtUrl};
                my $isPositive  = delete $thumb->{isPositive};
                my $pandoraType = delete $thumb->{pandoraType};
                my $songName    = delete $thumb->{songName};
                my $artistName  = delete $thumb->{artistName};
                if ($pandoraType eq 'TR') {
                    my $line = '-   ';
                    $line .= ':-) ' if $isPositive;
                    $line .= ':-( ' if !$isPositive;
                    $line .= $artistName . ' -- ' . $songName;
                    say $line;
                } else {
                    say '-   FEEDBACK TYPE: ' . $pandoraType;
                }
                if (scalar keys %$thumb) {
                    say '    -   unprocessed data: ', join(', ', sort keys %$thumb);
                    say '';
                }
            }
            say '';
        }
        if (scalar keys %$feedback) {
            say 'unprocessed station feedback data: ', join(', ', sort keys %$feedback);
            say '';
        }
    }

    if ($music) {
        my $artists = delete $music->{artists};
        my $songs   = delete $music->{songs};
        my $genres  = delete $music->{genres};
        if (scalar @$artists) {
            say '### Artists';
            say '';
            foreach my $artist (@$artists) {
                my $artistName = delete $artist->{artistName};
                my $pandoraType = delete $artist->{pandoraType};
                delete @$artist{qw(artUrl icon musicToken pandoraId seedId)};
                my $line = $artistName;
                $line .= sprintf(' [%s]', $pandoraType) if defined $pandoraType && $pandoraType ne 'AR';
                say '-   ' . $line;
                if (scalar keys %$artist) {
                    say '    -   unprocessed data: ', join(', ', sort keys %$artist);
                }
            }
            say '';
        }
        if (scalar @$songs) {
            say '### Songs';
            say '';
            foreach my $song (@$songs) {
                delete @$song{qw(artUrl musicToken pandoraId seedId)};
                my $artistName = delete $song->{artistName};
                my $songName = delete $song->{songName};
                my $pandoraType = delete $song->{pandoraType};
                my $line = $artistName . ' -- ' . $songName;
                $line .= sprintf(' [%s]', $pandoraType) if defined $pandoraType && $pandoraType ne 'TR';
                say '-   ' . $line;
                if (scalar keys %$song) {
                    say '    -   unprocessed data: ', join(', ', sort keys %$song);
                }
            }
            say '';
        }
        if (scalar @$genres) {
            say '### Genres';
            say '';
            foreach my $genre (@$genres) {
                delete $genre->{musicToken};
                delete $genre->{seedId};
                my $genreName = delete $genre->{genreName};
                say '-   ' . $genreName;
                if (scalar keys %$genre) {
                    say '    -   unprocessed data: ', join(', ', sort keys %$genre);
                }
            }
            say '';
        }
        if (scalar keys %$music) {
            say 'unprocessed station music data: ', join(', ', sort keys %$music);
            say '';
        }
    }

    if ($genre) {
        if (ref $genre eq 'ARRAY') {
            if (scalar @$genre) {
                say 'genre tags: ', join('; ', @$genre);
                say '';
            }
        } elsif ($genre) {
            say 'genre tags: ', $json->encode($genre);
            say '';
        }
    }

    if (scalar keys %$data) {
        say 'unprocessed station data: ', join(', ', sort keys %$data);
        say '';
    }
}

sub readFile {
    my ($filename) = @_;
    my $fh;
    open($fh, '<', $filename)        or die("open $filename: $!\n");
    binmode($fh, ':utf8')            or die("binmode $filename: $!\n");
    local $/ = undef;
    my $contents = <$fh>;
    close($fh)                       or die("close $filename: $!\n");
    my $result = $json->decode($contents);
    return $result;
}
